/*
 * This file is distributed under the terms in the attached LICENSE file.
 * If you do not find this file, copies can be found by writing to:
 * Intel Research Berkeley, 2150 Shattuck Avenue, Suite 1300,
 * Berkeley, CA, 94704.  Attention:  Intel License Inquiry.
 * Or
 * UC Berkeley EECS Computer Science Division, 387 Soda Hall #1776, 
 * Berkeley, CA,  94707. Attention: P2 Group.
 * 
 * DESCRIPTION: P2Chord in OverLog
 *
 * Execute first node with
 * tests/runOverLog -o doc/chord.olg -D LANDMARK=\"NIL\" -D LOCALADDRESS=\"<myhost>:<myport>\" -n <myhost> -p <myport> -D NODEID=<mynodeid>
 *
 * Execute joining nodes with
 * tests/runOverLog -o doc/chord.olg -D LANDMARK=\"<landmarkhost>:<landmarkport>\" -D LOCALADDRESS=\"<myhost>:<myport>\" -n <myhost> -p <myport> -D NODEID=<mynodeid>
 *
 * For example, the following starts node localhost:10000 with node ID 0 to join via landmark localhost:10101
 * tests/runOverLog -o doc/chord.olg -D LANDMARK=\"localhost:10101\" -D LOCALADDRESS=\"localhost:10000\" -n localhost -p 10000 -DNODEID=0x0I
 */

/* The base tuples */

materialize(node, infinity, 1, keys(1)).
materialize(landmark, infinity, 1, keys(1)).
materialize(uniqueFinger, 180, 160, keys(2)).
materialize(finger, 180, 160, keys(2)).
materialize(uniqueFinger, 180, 160, keys(2)).
materialize(bestSucc, 180, 1, keys(1)).
materialize(succ, 30, 100, keys(2)).
materialize(pred, infinity, 1, keys(1)).
materialize(join, 10, 5, keys(1)).
materialize(pendingPing, 10, infinity, keys(3)).  
materialize(fFix, 180, 160, keys(2)).  
materialize(nextFingerFix, 180, 1, keys(1)).  


/** constants */
#define SUCCESSORS 4
#define JOINRETRIES 3
#define JOINPERIOD 5
#define STABILIZEPERIOD 15
#define STABILIZEPERIOD 5
#define FINGERFIXPERIOD 10

/** Preexisting state. My landmark and myself. */
landmark(LOCALADDRESS, LANDMARK).
node(LOCALADDRESS, NODEID).

/** All nodes start with the nil predecessor */
pred(LOCALADDRESS, "NIL", "NIL").



watch(succ).
watch(stabilizeEvent).
watch(bestSucc).

/** Lookups */

l1 lookupResults(@R,K,S,SI,E) :- node(@NI,N), lookup(@NI,K,R,E),
	bestSucc(@NI,S,SI), K in (N,S].


l2 bestLookupDist(@NI,K,R,E,min<D>) :- node(@NI,N), 
					 lookup(@NI,K,R,E), 
					 finger(@NI,I,B,BI), 
					 D:=K - B - 1, 
					 B in (N,K).

l3 lookup(min<@BI>,K,R,E) :- node(@NI,N),	    
			       bestLookupDist(@NI,K,R,E,D), 
			       finger(@NI,I,B,BI), 
			       D == K - B - 1,
			       B in (N,K).

/** Neighbor Selection */

n0 newSuccEvent(@NI) :- succ(@NI,S,SI).

n2 newSuccEvent(@NI) :- deleteSucc(@NI,S,SI).

n1 bestSuccDist(@NI,min<D>) :- newSuccEvent(@NI), node(@NI,N),
	succ(@NI,S,SI), D:=S - N - 1.

n3 bestSucc(@NI,S,SI) :- succ(@NI,S,SI), bestSuccDist(@NI,D),
	node(@NI,N), D == S - N - 1.

n4 finger(@NI,0,S,SI) :- bestSucc(@NI,S,SI).


/** Successor eviction */

s1 succCount(@NI,count<*>) :- newSuccEvent(@NI),
				succ(@NI,S,SI).

s2 evictSucc(@NI) :- succCount(@NI,C), 
		    C > SUCCESSORS.

s3 maxSuccDist(@NI,max<D>) :- succ(@NI,S,SI),
				node(@NI,N), 
				evictSucc(@NI),
				D:=S - N - 1.

s4 delete succ(@NI,S,SI) :- node(@NI,N), succ(@NI,S,SI),
	maxSuccDist(@NI,D), D == S - N - 1.


/** Finger fixing */

f1 fFix(@NI,E,I) :- periodic(@NI,E,FINGERFIXPERIOD),
	nextFingerFix(@NI,I).

f2 fFixEvent(@NI,E,I) :- fFix(@NI,E,I).

f3 lookup(@NI,K,NI,E) :- fFixEvent(@NI,E,I), node(@NI,N), K:=0x1I << I +
	N.

f4 eagerFinger(@NI,I,B,BI) :- fFix(@NI,E,I),
	lookupResults(@NI,K,B,BI,E).

f5 finger(@NI,I,B,BI) :- eagerFinger(@NI,I,B,BI).

f6 eagerFinger(@NI,I,B,BI) :- node(@NI,N), eagerFinger(@NI,I1,B,BI),
	I:=I1 + 1, K:=0x1I << I + N, K in (N,B), BI != NI.

f7 delete fFix(@NI,E,I1) :- eagerFinger(@NI,I,B,BI), fFix(@NI,E,I1), I >
	0, I1 == I - 1.

f8 nextFingerFix(@NI,0) :- eagerFinger(@NI,I,B,BI), 
			   ((I == 159) || (BI == NI)).

f9 nextFingerFix(@NI,I) :- node(@NI,N), 
			   eagerFinger(@NI,I1,B,BI), 
			   I:=I1 + 1, 
			   K:=0x1I << I + N,
			   K in (B,N), 
			   NI != BI.

f10 uniqueFinger(@NI,BI) :- finger(@NI,I,B,BI).


/** Churn Handling */


/* Insert in entries after a delay. Change join. */

c1 joinEvent(@NI,E) :- periodic(@NI, E, JOINPERIOD, JOINRETRIES).

c2 join(@NI,E) :- joinEvent(@NI,E).

c3 joinReq(@LI,N,NI,E) :- joinEvent(@NI, E),
	node(@NI, N), 
	landmark(@NI, LI),
	LI != "NIL".

c4 succ(@NI,N,NI) :- landmark(@NI, LI),
	joinEvent(@NI, E),
	node(@NI, N), 
	LI == "NIL".

c5 lookup(@LI,N,NI,E) :- joinReq(@LI,N,NI,E).

c6 succ(@NI,S,SI) :- join(@NI, E), lookupResults(@NI,K,S,SI,E).



/** Stabilization */


sb0 stabilizeEvent(@NI) :- periodic(@NI, E, STABILIZEPERIOD).

sb1 succ(@NI,P,PI) :- stabilizeEvent(@NI), node(@NI,N),
	bestSucc(@NI,S,SI), pred(@SI,P,PI), PI != "NIL", P in (N,S).

sb2 succ(@NI,S1,SI1) :- stabilizeEvent(@NI), succ(@NI,S,SI),
	succ(@SI,S1,SI1).

sb3 pred(@SI,N,NI) :- stabilizeEvent(@NI), node(@NI,N), succ(@NI,S,SI),
	pred(@SI,P,PI), node(@SI,N1), ((PI == "NIL") || (N in (P,N1))).



/** Ping Nodes */

pp1 pendingPing(@NI,SI,E1,T) :- periodic(@NI,E,PINGPERIOD),
			 succ(@NI,S,SI), E1:=f_rand(),
                         SI != NI, T:=f_now().

pp2 pendingPing(@NI,PI,E1,T) :- periodic(@NI,E,PINGPERIOD),
			  pred(@NI,P,PI), E1:=f_rand(),
                          PI != "NIL", T:=f_now().

pp3 pendingPing(@NI,FI,E1,T) :- periodic(@NI,E,PINGPERIOD),
	uniqueFinger(@NI,FI), E1:=f_rand(),
	T:=f_now().

pp4 pingResp(@RI,NI,E) :- pingReq(@NI,RI,E).

pp5 pingReq(@PI,NI,E) :- periodic(@NI,E1,3), pendingPing(@NI,PI,E,T).

pp6 delete pendingPing(@NI,SI,E,T) :- pingResp(@NI,SI,E), 
                                      pendingPing(@NI,SI,E,T).
                                      

/** Failure Detection */

cm1 nodeFailure(@NI,PI,E1,D) :- periodic(@NI,E,1), pendingPing(@NI,PI,E1,T), 
                           T1:=f_now(), D := T1 - T, D > 7.

cm1a delete pendingPing(@NI,PI,E,T) :- nodeFailure(@NI,PI,E,D), 
                                       pendingPing(@NI,PI,E,T).

cm2a deleteSucc(@NI,S,SI) :- succ(@NI,S,SI), nodeFailure(@NI,SI,E,D).
cm2b delete succ(@NI,S,SI) :- deleteSucc(@NI,S,SI).

cm3 pred(@NI,"NIL","NIL") :- pred(@NI,P,PI), nodeFailure(@NI,PI,E,D).

cm4 delete finger(@NI,I,B,BI) :- finger(@NI,I,B,BI), nodeFailure(@NI,BI,E,D).

cm6 delete uniqueFinger(@NI,FI) :- uniqueFinger(@NI,FI), nodeFailure(@NI,FI,E,D).

